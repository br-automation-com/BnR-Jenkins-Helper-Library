apply plugin: 'groovy'

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://repo.jenkins-ci.org/releases/' }
}

def jenkinsVersion = "2.432"
def allureVersion = "2.24.0"
def aspectJVersion = "1.9.20.1"
// Define configuration for AspectJ agent
configurations {
    agent {
        canBeResolved = true
        canBeConsumed = true
    }
}

dependencies {
    // Jenkins
    implementation group: 'org.jenkins-ci.main', name: 'jenkins-core', version: "$jenkinsVersion"
    testImplementation group: 'org.jenkins-ci.main', name: 'jenkins-core', version: "$jenkinsVersion"

    // Test frameworks
    implementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '3.0.20'
    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testImplementation group: 'com.lesfurets', name: 'jenkins-pipeline-unit', version: '1.8'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.5.13'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.10.1'
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.8.1'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.1'

    testImplementation platform("io.qameta.allure:allure-bom:$allureVersion")
    testImplementation "io.qameta.allure:allure-junit5"
    agent "org.aspectj:aspectjweaver:$aspectJVersion"

    // Jenkins plugins
    implementation group: 'com.cloudbees', name: 'groovy-cps', version: '1.32'
    implementation group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-step-api', version: '2.22'
    implementation group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-api', version: '2.40'
    implementation group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-cps', version: '2.83'
    implementation group: 'org.jenkins-ci.plugins', name: 'powershell', version: '2.1'
}

sourceSets {
  main {
    groovy {
      srcDirs = ['src', 'vars']
    }
  }
  test {
    groovy {
      srcDirs = ['test']
    }
  }
}

test {
  useJUnitPlatform()
  jvmArgs = [ "-javaagent:${configurations.agent.singleFile}" ]
}